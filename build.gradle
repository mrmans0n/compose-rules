// Copyright 2023 Nacho Lopez
// SPDX-License-Identifier: Apache-2.0
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    alias(libs.plugins.kotlin.jvm) apply false
    alias(libs.plugins.mavenPublish) apply false
    alias(libs.plugins.spotless) apply false
}

allprojects {
    pluginManager.apply(libs.plugins.spotless.get().pluginId)
    spotless {
        kotlin {
            target "**/*.kt"
            ktlint("1.0.0")
            // NOTE: When ktlint supports 1.1.0, use this instead:
            // ktlint(libs.versions.ktlint.get())

            licenseHeaderFile rootProject.file('spotless/copyright.txt')
        }
        groovyGradle {
            target "**/*.gradle"
            greclipse().configFile(rootProject.file("spotless/greclipse.properties"))
            licenseHeaderFile rootProject.file('spotless/copyright.txt'), '(buildscript|apply|import|plugins)'
        }
    }

    plugins.withType(JavaBasePlugin).configureEach {
        java {
            toolchain.languageVersion = JavaLanguageVersion.of(11)
        }
    }

    tasks.withType(KotlinCompile).configureEach {
        compilerOptions {
            // Treat all Kotlin warnings as errors
            allWarningsAsErrors = true
            freeCompilerArgs.addAll(
                    // Enable default methods in interfaces
                    "-Xjvm-default=all",
                    // Enable context receivers
                    "-Xcontext-receivers",
                    // Enable K2 compiler
                    "-language-version=2.0",
                    "-Xsuppress-version-warnings",
                    )
        }
    }

    version = VERSION_NAME
    if (project != rootProject) {
        pluginManager.apply(libs.plugins.mavenPublish.get().pluginId)
    }
}

tasks.register("printVersion") {
    doLast {
        println(project.property("VERSION_NAME"))
    }
}
